description = "grpc-java: OpenTracing"

group = "io.opentracing.contrib"
version = '0.3.1-corvana'

apply plugin: 'java'
apply plugin: 'maven'

ext.corvanaCreds = new XmlSlurper().parse(new File(System.getProperty("user.home"), ".m2/settings.xml"))."servers"."server"
  .find { it."id".text() == "repo.corvana.com" }

ext.mavenUrl = "${System.properties.getOrDefault('corvana.nexus.baseurl', 'https://repo.corvana.com')}/repository/maven/"
ext.mavenReleases = "${System.properties.getOrDefault('corvana.nexus.baseurl', 'https://repo.corvana.com')}/repository/corvana-releases/"
ext.mavenSnapshots = "${System.properties.getOrDefault('corvana.nexus.baseurl', 'https://repo.corvana.com')}/repository/corvana-snapshots/"

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java', 'src/testgen']
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

repositories {
    maven {
        credentials {
            username corvanaCreds.username.text()
            password corvanaCreds.password.text()
        }
        url mavenUrl
    }
    maven {
        url uri("${System.getProperty('user.home')}/.m2/repository")
    }
}

jar {
    baseName 'grpc-opentracing'
    version = '0.3.1-corvana'
}

dependencies {
    compile 'io.grpc:grpc-core:1.7.0'
    compile 'io.opentracing:opentracing-api:0.31.0'
    testCompile 'io.opentracing:opentracing-mock:0.31.0'
    testCompile 'io.grpc:grpc-protobuf:1.7.0'
    testCompile 'io.grpc:grpc-netty:1.7.0'
    testCompile 'io.grpc:grpc-stub:1.7.0'
    testCompile 'junit:junit:4.12'
}

// Allow for automatic promotion and release to Maven Central
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}
apply plugin: 'io.codearte.nexus-staging'

nexusStaging {
    packageGroup = "io.opentracing"
}

test {
    maxParallelForks = 1
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: mavenReleases) {
            authentication(userName: corvanaCreds.username.text(), password: corvanaCreds.password.text())
        }
        snapshotRepository(url: mavenSnapshots) {
            authentication(userName: corvanaCreds.username.text(), password: corvanaCreds.password.text())
        }
    }
}
